pi = 4 * a(1)		/* Die Zahl pi */

define int(x) {		/* berechnet den ganzzahligen Anteil */
	auto sc, y;

	sc = scale;
	scale = 0;
	y = x/1.0;
	scale = sc;
	return(y);
}

define abs(x) {		/* Betrag */
	if(x < 0) {
		return(-x);
	}
	return(x);
}

define round(x) {	/* rundet */
	return(int(x + .5));
}

define fac(n) {		/* berechnet n! */	
	if(n < 0) {
		print "fac(",n,") geht nicht!\n";
		halt;	
	}	
	if(n != int(n)) {
		print "fac(",n,") geht nicht!\n";
		halt;	
	}
	if(n == 0) {
		return(1);
	}
	return(n*fac(n-1));
}

define fak(n) {		/* berechnet n! */	
	return(fac(n));
}

define binom(n, k) {	/* n ueber k */
	if(k == 0) {
		return(1);
	}
	if(k == n) {
		return(1);
	}
	if(k > (n - k)) {
		k = n - k;
	}
	return(fac(n)/(fac(k)*fac(n-k)));
}

define atan(x) {	/* arcus tangens */
	return(a(x));
}

define exp(x) {		/* Exponentialfunktion */
	return(e(x));
}

define sin(x) {		/* Sinus */
	return(s(x));
}

define cos(x) {		/* Cosinus */
	return(c(x));
}

define ln(x) {		/* natuerlicher Logarithmus */
	return(l(x));
}

define log(x) {		/* natuerlicher Logarithmus */
	return(l(x));
}

define log10(x) {		/* Briggscher Logarithmus */
	return(l(x)/l(10));
}

define tan(x) {		/* Tangens */
	auto denom;

	denom = c(x);
	if(denom != 0) {
		return(sin(x)/denom);
	}
	print "tan(",x,") = infinity\n"
	halt;	
}

define pow(x, y) {		/* x^y */
	auto exp;

	if (y == int(y)) {
		return(x^y);
	}

	if (x < 0) {
		print "basis \q ",x," < 0 \q bei nichtganzem Exponenten. Geht nicht!\n";
		halt;
	}

	if (y == 0) return(1);

	return(e(y * l(x)));
}

define asin(x) {		/* arcus sinus */
	if(abs(x) > 1) {
		print "asin(",x,") nicht definiert!\n";
		halt;
	}
	if(x == 1) {
		return(pi/2);
	}	
	if(x == -1) {
		return(-pi/2);
	}	
	return(a(x/sqrt(1 - x*x)));
}

define acos(x) {		/* arcus cosinus */
	return(pi/2 - asin(x));
}

define sinh(x) {		/* sinus hyperbolicus */
	auto z; 
	
	z = e(x);
	return((z - 1/z)/2);
}	

define cosh(x) {		/* cosinus hyperbolicus */
	auto z;

	z = e(x);
	return((z + 1/z)/2);
}	
